class_name InventoryUI
extends Panel

@export var slots : Array[InventorySlotUI]
@onready var inventory : Inventory = $"../../Inventory"
@onready var player : Player = $"../.."
@onready var info_panel : Panel = $"../ItemInfoPanel"
@onready var info_panel_name : Label = $"../ItemInfoPanel/ItemName"
@onready var info_panel_description : Label = $"../ItemInfoPanel/ItemDescriptionText"

func _ready() -> void:
	inventory.UpdatedInventory.connect(_update_ui)
	_update_ui()
	_hide_info_panel()
	
	for slot in slots:
		slot.mouse_entered.connect(func(): _set_info_panel(slot))
		slot.mouse_exited.connect(_hide_info_panel)
	
	

func _process(delta: float) -> void:
	if not info_panel.visible:
		return
	
	var mouse_pos:Vector2 = get_viewport().get_mouse_position()
	info_panel.global_position.y = mouse_pos.y - info_panel.size.y
	info_panel.global_position.x = mouse_pos.x + 5
	

func _update_ui():
	for i in len(slots):
		slots[i].set_item_slot(inventory.item_slots[i], player)

func _set_info_panel (slot: InventorySlotUI):
	var item = slot.item_slot.item
	
	if not item:
		return
	info_panel.visible = true
	info_panel_name.text = item.display_name
	info_panel_description.text = item.description

func _hide_info_panel ():
	info_panel.visible = false
